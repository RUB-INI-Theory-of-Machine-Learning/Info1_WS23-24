# Configuration
var playfield_size  = {"x": 10, "y": 20};

class Playfield {
public:
	var bitmap = [];
	var width, height;

	constructor(width, height){
		this.width = width;
		this.height = height;
		
		this.bitmap.push(Array(width + 2, "f"));
		for 0:height do {
			var row = ["f"];
			for 0:width do row.push(0);
			row.push("f");
			this.bitmap.push(row);
		};
		this.bitmap.push(Array(width + 2, "f"));
	}

	function insert_tetromino(tetromino){
		var bitmap = tetromino.bitmap();
		for var i in 0:bitmap.size() do
			for var j in 0:bitmap[i].size() do
				if bitmap[i][j] != 0 then this.bitmap[tetromino.row + i][tetromino.column + j] = bitmap[i][j];
	}

	function remove_tetromino(tetromino){
		var bitmap = tetromino.bitmap();
		for var i in 0:bitmap.size() do
			for var j in 0:bitmap[i].size() do
				if bitmap[i][j] != 0 then this.bitmap[tetromino.row + i][tetromino.column + j] = 0;
	}

	function check_collision(tetromino){
		var bitmap = tetromino.bitmap();
		for var i in 0:bitmap.size() do {
			for var j in 0:bitmap[i].size() do {
				if bitmap[i][j] != 0 then
					if this.bitmap[tetromino.row + i][tetromino.column + j] != 0 then
				return true;
			}
		}
		return false;
	}
	
	function insert_rows(number){
		var hole = math.floor(math.random() * this.width);
		for 0:number do {
			this.bitmap.remove(1);
			
			var row = [];
			row.push("f");
			for var column in 0:this.width do {
				if column == hole then row.push(0);
				else row.push("n");
			}
			row.push("f");
			this.bitmap.insert(this.height, row);
		}
	}
	
	function remove_full_rows(){
		var full_rows = [];
		
		for var row in 1:this.bitmap.size()-1 do {
			var is_full = true;
			for var column in 0:this.bitmap[row].size() do {
				if this.bitmap[row][column] == 0 then {
					is_full = false;
					break;
				}
				# if column == this.bitmap[row].size()-1 then full_rows.push(row);
				
			}
			if is_full then full_rows.push(row);
		}
		
		
		for var row_idx in full_rows do {
			this.bitmap.remove(row_idx);
			var row = ["f"];
			for 0:this.width do row.push(0);
			row.push("f");
			this.bitmap.insert(1, row);
		}
		
		return full_rows.size();
	}
}


class Tetromino {
public:
	var column, row, rotation, type, max_rotations, previous_state;

	static var tetrominos = {
    "o": [[
            [0,0,0,0],
            [0,"o","o",0],
            [0,"o","o",0],
            [0,0,0,0],
        ]],
    "t":[
            # The first rotation of the tetromino
            [
                [ 0,  0,  0, 0 ], # the first row
                ["t","t","t",0 ], # the second row
                [ 0, "t", 0, 0 ]  # the third row
            ],
            # The other three rotations
            [[0,"t",0,0],["t","t",0,0],[0,"t",0,0]],
            [[0,"t",0,0],["t","t","t",0],[0,0,0,0]],
            [[0,"t",0,0],[0,"t","t",0],[0,"t",0,0]]
        ],
     "s":[
            [[0,0,0,0],[0,"s","s",0],["s","s",0,0]],
            [["s",0,0,0],["s","s",0,0],[0,"s",0,0]]
        ],
    "z":[
            [[0,0,0,0],["z","z",0,0],[0,"z", "z",0]],
            [[0,"z",0,0],["z","z",0,0],["z",0,0,0]]
        ],
    "j":[
            [[0,0,0,0],["j","j","j",0],[0,0,"j",0]],
            [[0,"j",0,0],[0,"j",0,0],["j","j",0,0]],
            [["j",0,0,0],["j","j","j",0],[0,0,0,0]],
            [[0,"j","j",0],[0,"j",0,0],[0,"j",0,0]]
        ],
    "l":[
            [[0,0,0,0],["l","l","l",0],["l",0,0,0]],
            [["l","l",0,0],[0,"l",0,0],[0,"l",0,0]],
            [[0,0,"l",0],["l","l","l",0],[0,0,0,0]],
            [[0,"l",0,0],[0,"l",0,0],[0,"l","l",0]]
        ],
    "i":[
            [[0,0,0,0],[0,0,0,0],["i","i","i","i"],[0,0,0,0]],
            [[0,"i",0,0],[0,"i",0,0],[0,"i",0,0],[0,"i",0,0]],
        ]
};

	constructor(type, column, row = 1, rotation = 0){
		this.column = column;
		this.row = row;
		this.rotation = rotation;
		this.type = type;
		this.max_rotations = this.tetrominos[this.type].size();
		save_state();
	}

	function bitmap(){
		return this.tetrominos[this.type][this.rotation];
	}

	# Save state of a tetromino so we can revert to it 
	# in case we did an action that caused a collision
	function save_state() {
		this.previous_state = {
			"row": this.row,
			"column": this.column,
			"rotation": this.rotation,
		};
	}

	# revert to previously saved state
	function undo(){
		this.row = previous_state["row"];
		this.column = previous_state["column"];
		this.rotation = previous_state["rotation"];
	}

	function drop(){
		save_state();
		this.row = this.row + 1;
	}
	
	function rotate_left() {
		save_state();
		this.rotation = (this.rotation + 1) % this.max_rotations;
	}
	
	function rotate_right() {
		save_state();
		this.rotation = (this.rotation - 1) % this.max_rotations;
	}
	
	function move_left() {
		save_state();
		this.column -= 1;
	}
	
	function move_right() {
		save_state();
		this.column += 1;
	}
}

# We collect all the tools needed to draw the playfield matrix
# on the canvas here in this Renderer class
class Renderer {
public:
	var playfield_width, playfield_height;
	var block_size, outline_size;
	var x, y, width, height;
	
	var sidebar_size = 7;
	
	var p_bitmap, p_score, p_level, p_lines, p_tetromino;
	# A dictionary of dictionaries of arrays containing the RGB values for the colors.
	var colors = {
		"f": {
			"base": [119, 119, 119],
			"light": [153, 153, 153],
			"dark": [51, 51, 51],
		},
		"o": { # yellow
			"base": [204, 204, 0],
			"light": [255, 255, 0],
			"dark": [153, 153, 0],
		},
		"t": { # violet
			"base": [153, 0, 204],
			"light": [204, 0, 255],
			"dark": [102, 0, 153],
		},
		"s": {
			"base": [0, 204, 0],
			"light": [0, 255, 0],
			"dark": [0, 153, 0],
		},
		"z": {
			"base": [204, 0, 0],
			"light": [255, 0, 0],
			"dark": [153, 0, 0],
		},
		"j": {
			"base": [0, 0, 204],
			"light": [0, 0, 255],
			"dark": [0, 0, 153],
		},
		"l": {
			"base": [204, 102, 0],
			"light": [255, 136, 0],
			"dark": [153, 68, 0],
		},
		"i": {
			"base": [0, 204, 204],
			"light": [0, 255, 255],
			"dark": [0, 153, 153],
		},
		"n": {
			"base": [149, 149, 149],
			"light": [183, 183, 183] ,
			"dark": [81, 81, 81]
		},
	};

	constructor(playfield_width, playfield_height, x, y, width, height){
		this.playfield_width = playfield_width;
		this.playfield_height = playfield_height;
		
		this.x = math.floor(x);
		this.y = math.floor(y);
		this.width = math.floor(width);
		this.height = math.floor(height);
		
		# Define block size for a field of 10x20.
		# We calculate the vertical and horizontal block size and take the minimum.
		# This way we can guarantee, that the playfield is always completely visible.
		this.block_size = math.floor(math.min(
		this.height/(playfield_size["y"]+2),
		this.width/(playfield_size["x"]+2 + this.sidebar_size)
		));

		# We define the outline to be 1/8 of the block size
		this.outline_size = this.block_size/8;
		
		# set background color of the canvas once
		canvas.setFillColor(0, 0, 0);
		canvas.fillRect(this.x , this.y , this.width, this.height);
		
		
		# draw sidebar once
		for var column in 0:this.sidebar_size do {
			for var row in 0:playfield_height+2 do {
				if (row == 0 or row == playfield_height+1 or column == this.sidebar_size-1) then
				
				this.draw_block(row, column + playfield_width + 1, "f");
			}
		}
		
		canvas.setFillColor(1,1,1);
		canvas.setFont("Comic Sans MS", block_size);
		canvas.text(this.x + block_size * (this.playfield_width+3), this.y + 2* block_size, "Score");
		canvas.text(this.x + block_size * (this.playfield_width+3), this.y + 5* block_size, "Lines");
		canvas.text(this.x + block_size * (this.playfield_width+3), this.y + 8* block_size, "Level");
		
	}

	function render(playfield, score, lines, level, next_tetromino) {
		var bitmap_diff = [];
		if this.p_bitmap == null then {
			bitmap_diff = playfield.bitmap;
		}
		else {
			for var row in 0:playfield.bitmap.size() do {
				var diff_row = [];
				for var column in 0:playfield.bitmap[row].size() do {
					if playfield.bitmap[row][column] != this.p_bitmap[row][column] then {
						if playfield.bitmap[row][column] == 0 then diff_row.push(" ");
						else diff_row.push(playfield.bitmap[row][column]);
					}
					else diff_row.push(0);
				}
				bitmap_diff.push(diff_row);
			}
		}
		this.p_bitmap = deepcopy(playfield.bitmap);
		this.draw_playfield(bitmap_diff);
		
		if next_tetromino != this.p_tetromino then {
			this.p_tetromino = next_tetromino;
			this.draw_next_tetromino(Tetromino.tetrominos[next_tetromino][0]);
		}
		
		if score != this.p_score then {
			this.p_score = score;
			this.draw_text(score, 3);
		}
		if lines != this.p_lines then {
			this.p_lines = lines;
			this.draw_text(lines, 6);
		}
		if level != this.p_level then {
			this.p_level = level;
			this.draw_text(level, 9);
		}
		
	}
	
	function draw_next_tetromino(bitmap){
		canvas.setFillColor(0,0,0);
		canvas.fillRect(this.x + block_size * (this.playfield_width + 2.5) ,this.y + 16 * block_size, 4 * block_size, 4 * block_size);
		
		for var column in 0:bitmap.size() do
			for var row in 0:bitmap[column].size() do {
				if (bitmap[column][row] != 0) then this.draw_block(
				column + 16, 
				row + 2.5 + this.playfield_width, 
				bitmap[column][row]
				);
			}

	}
	
	function draw_playfield(bitmap){
		for var column in 0:bitmap.size() do
			for var row in 0:bitmap[column].size() do {
				if (bitmap[column][row] == " ") then this.draw_background(column, row);
				else if (bitmap[column][row] != 0) then this.draw_block(column, row, bitmap[column][row]);
			}
	}
	
	function draw_text(text, row){
		canvas.setFillColor(0,0,0);
		canvas.fillRect(this.x + block_size * (this.playfield_width + 3) , this.y + row * block_size -1, 4 * block_size, 2 * block_size);
		
		canvas.setFillColor(1,1,1);
		canvas.text(this.x + block_size * (this.playfield_width + 3), this.y + row * block_size, text);
	}
	
	function draw_background(column, row){
		canvas.setFillColor(0, 0, 0);
		canvas.fillRect(this.x + row * block_size, this.y + column * block_size, block_size, block_size);
	}

	# This is the function that draws the block for us. It takes pos_x and pos_y as arguments.
	# Both are in the unit of blocks, as the conversion to pixels happens in this function.
	# The blocks get drawn as a square with light color, a triangle with darker color and a smaller square with base color on top of each other
	function draw_block(column, row, color) {
		var pos_x = x + row * block_size;
		var pos_y = y + column * block_size;

		# lighter edges
		canvas.setFillColor(colors[color]["light"][0]/255,
							colors[color]["light"][1]/255,
							colors[color]["light"][2]/255);
		canvas.fillRect(pos_x, pos_y, block_size, block_size);

		# triangle with dark color for the area bottom right
		canvas.setFillColor(colors[color]["dark"][0]/255,
							colors[color]["dark"][1]/255,
							colors[color]["dark"][2]/255);
		canvas.fillArea([
							[pos_x + block_size, pos_y],
							[pos_x, pos_y + block_size],
							[pos_x + block_size, pos_y + block_size]
						]);

		# smaller square in the middle
		canvas.setFillColor(colors[color]["base"][0] / 255,
							colors[color]["base"][1] / 255,
							colors[color]["base"][2]/255);
		canvas.fillRect(
			pos_x+outline_size,
			pos_y+outline_size,
			block_size-2*outline_size,
			block_size-2*outline_size
		);
	}
}

# Our random tetromino generator
class TetrominoPool{
	public:
	var pool = [];

	constructor(){
		this.fill_pool();
	}
	function fill_pool(){
		var pool_ = ["o", "s", "z", "i", "j", "l", "t"];

	 	while pool_.size() > 0 do {
			var rand_idx = Integer((math.random() * pool_.size()));
			this.pool.push(pool_[rand_idx]);
			pool_.remove(rand_idx);
		}
	}

	function get(){
		var tetromino = this.pool.pop();
		if this.pool.size() == 0 then this.fill_pool();
		return tetromino;
	}
	
	function next(){
		return this.pool[this.pool.size()-1];
	}
}


class Game {
	public:
	var incoming_lines = 0;
	var outgoing_lines = 0;
	
	var base_score_values = [0, 40, 100, 300, 1200];
	var level_gravity = [
		887, 	#level 0
		820,  	#level 1
		753,	#level 2
		686,	#level 3
		619,	#level 4
		552,	#level 5
		469,	#level 6
		368,	#level 7
		285,	#level 8
		184,	#level 9
		167, 	#level 10
		151,	#level 11
		134,	#level 12
		117,	#level 13
		100,	#level 14
		100,	#level 15
		84,	 	#level 16
		84,		#level 17
		67,		#level 18
		67,		#level 19
		50,		#level 20
	];
	
	var level, lines, score, soft_drop_start, gravity, current_time, spawned;
	var bindings;
	var pool, tetromino, playfield, renderer;

	constructor(v_canvas = [0,0,0,0], bindings=["ArrowUp", "ArrowDown", "ArrowLeft", "ArrowRight", " "]){
		this.bindings = bindings;
		
		if(v_canvas[2] == 0) then v_canvas[2] = canvas.width();
		if(v_canvas[3] == 0) then v_canvas[3] = canvas.height();
		
		this.level = 9;
		this.lines = 0;
		this.score = 0;
		this.soft_drop_start = 0;
		this.gravity = level_gravity[this.level];
		this.current_time = time();
		this.spawned = false;
	
		
		this.pool = TetrominoPool();
		this.tetromino = Tetromino(this.pool.get(), Integer((playfield_size["x"] + 2)/2 - 2));
		this.playfield = Playfield(playfield_size["x"], playfield_size["y"]);
		this.renderer = Renderer(playfield_size["x"], playfield_size["y"], v_canvas[0], v_canvas[1], v_canvas[2], v_canvas[3]);
	}
	
	function step(){
		if time() - this.current_time > this.gravity then {
			# reset time buffer
			this.current_time = time();

			# remove the tetromino from the playfield to do a "preliminary" drop. Before we insert the tetromino back into the playfield we have to check if that drop was legal. 
			this.playfield.remove_tetromino(this.tetromino);
			this.tetromino.drop();
			if this.playfield.check_collision(this.tetromino) == true then {
				# that drop caused a collision, i.e. at least two blocks overlap. 
				this.tetromino.undo();
				this.playfield.insert_tetromino(this.tetromino);
				
				var cleared_rows = this.playfield.remove_full_rows();
				if cleared_rows > 0 then {
					this.lines = this.lines + cleared_rows;
					if this.lines // 10 > this.level and this.level < this.level_gravity.size() then this.level = this.lines // 10;
					var soft_drop_points = 0;
					if this.soft_drop_start != 0 then {
						soft_drop_points = this.tetromino.row - this.soft_drop_start;
					}
					this.score = this.score + this.base_score_values[cleared_rows] * (this.level + 1) + soft_drop_points;

					this.soft_drop_start = 0;
					this.gravity = this.level_gravity[this.level];
					
					if cleared_rows < 4 then this.outgoing_lines += cleared_rows - 1;
					else this.outgoing_lines += 4;
				}
				
				if this.incoming_lines > 0 then {
					this.playfield.insert_rows(this.incoming_lines);
					this.incoming_lines = 0;
				}
				
				# spawn a new tetromino
				this.tetromino = Tetromino(this.pool.get(), Integer((playfield_size["x"] + 2)/2 - 2));
				if this.spawned then {
					# We got a collision right after we spawned a new tetromino. Game over.
					this.playfield.insert_tetromino(this.tetromino);
					this.renderer.render(this.playfield, this.score, this.lines, this.level, this.pool.next());
					throw "Game Over";
				}

				this.spawned = true;
			}
			else this.spawned = false; # the new tetromino dropped successfully, so it is just a normal collision in the next step (not a spawn collision)
			this.playfield.insert_tetromino(this.tetromino);
			this.renderer.render(this.playfield, this.score, this.lines, this.level, this.pool.next());
		}
	}
	
	function keydown(key){
		this.playfield.remove_tetromino(this.tetromino);
	
		if key == this.bindings[4] then {
			if this.soft_drop_start == 0 then this.soft_drop_start = Integer(this.tetromino.row);
				this.gravity = 50;
			};
			if key == this.bindings[0] then this.tetromino.rotate_left();
			if key == this.bindings[1] then this.tetromino.rotate_right();
			if key == this.bindings[2] then this.tetromino.move_left();
			if key == this.bindings[3] then this.tetromino.move_right();
			if this.playfield.check_collision(this.tetromino) then {
				this.tetromino.undo(); # The action caused an invalid playfield state. Undo it. 
		}

		this.playfield.insert_tetromino(this.tetromino);
		this.renderer.render(this.playfield, this.score, this.lines, this.level, this.pool.next());
	}
	
	function keyup(key){
		if key == this.bindings[4] and this.soft_drop_start != 0 then {
			this.soft_drop_start = 0;
			this.gravity = this.level_gravity[this.level];
		}
	}
	
	function get_incoming_lines(){
		return this.incoming_lines;	
	}
	
	function add_incoming_lines(lines){
		this.incoming_lines += lines;
	}
	
	function get_outgoing_lines(){
		return this.outgoing_lines;	
	}
	
	function set_outgoing_lines(lines){
		this.outgoing_lines = lines;
	}
}

class Bot {
	public:
	var current_time, speed;
	var optimal_position, current_tetromino, next_tetromino;
	var game;
	
	constructor(game, speed=100){
		this.game = game;
		this.speed = speed;
		this.current_time = time() + speed/2;
	}
	
	function step(){
		this.game.step();
		
		if time() - this.current_time > this.speed then {
			# reset time buffer
			this.current_time = time();
			
			if this.optimal_position == null or 
				(
					this.current_tetromino != this.game.tetromino.type or  
				 	this.next_tetromino != this.game.pool.next()
				 )
			then {
				this.current_tetromino = this.game.tetromino.type;
				this.next_tetromino = this.game.pool.next();
				
				var tetromino = Tetromino(this.game.tetromino.type, this.game.tetromino.column);
				var playfield = Playfield(this.game.playfield.width, this.game.playfield.height);
				playfield.bitmap = deepcopy(this.game.playfield.bitmap);
				playfield.remove_tetromino(tetromino);
				this.optimal_position = this.find_optimal_position(tetromino, playfield);
				print("optimal position found");
			}
			
			
			if this.game.tetromino.rotation < this.optimal_position["rotation"] then this.game.keydown(this.game.bindings[0]);
			else if this.game.tetromino.rotation > this.optimal_position["rotation"] then this.game.keydown(this.game.bindings[1]);
			else if this.game.tetromino.column > this.optimal_position["column"] then this.game.keydown(this.game.bindings[2]);
			else if this.game.tetromino.column < this.optimal_position["column"] then this.game.keydown(this.game.bindings[3]);
			
		}
	}
	
	function find_optimal_position(tetromino, playfield){
		var positions = [];
		
		for var rot_idx in 0:tetromino.max_rotations do {
			tetromino.rotation = rot_idx;
			
			var first_el = null;
			for var col_idx in 0:4 do {
				for var row_idx in 0:tetromino.bitmap().size() do {
					if(tetromino.bitmap()[row_idx][col_idx] != 0) then {
						first_el = col_idx;
						break;
					}
				}
				if (first_el != null) then break;
			} 
			
			for var col_idx in (1 - first_el):playfield.bitmap[0].size()-1 do {
				tetromino.column = col_idx;
				
				if playfield.check_collision(tetromino) then {
					break;
				}
				
				while not playfield.check_collision(tetromino) do {
					tetromino.row += 1;
				}
				
				tetromino.row -= 1;
				
				playfield.insert_tetromino(tetromino);
				var score = this.score(playfield);
				positions.push(
					{
						"score": score,
						"column": tetromino.column,
						"rotation": tetromino.rotation
					}
				);
				playfield.remove_tetromino(tetromino);				
				tetromino.row = 1;

			}
		}
		
		var optimal_position = positions[0];

		for var position_idx in 1:positions.size() do {
			if positions[position_idx]["score"] < optimal_position["score"] then {
				optimal_position = positions[position_idx];
			}
		}

		return optimal_position;
	}
	
	function score(playfield){
		var weights = {
			"height": 1,
			"holes": 2,
			"roughness": 1
		};
		
		return (
			this.height(playfield.bitmap) * weights["height"] + 
			this.holes(playfield.bitmap) * weights["holes"] + 
			this.roughness(playfield.bitmap) * weights["roughness"]
			);
	}
	
	function roughness(playfield){
		var playfield_heights = [];
		
		for var col_idx in 1:playfield[0].size()-1 do {
			for var row_idx in 1:playfield.size()-1 do {
				if playfield[row_idx][col_idx] != 0 then {
					playfield_heights.push(row_idx);
					break;
				}
			}
		}

		var score = 0;
		
		for var height_idx in 0:playfield_heights.size()-1 do {
			score += math.abs(playfield_heights[height_idx] - playfield_heights[height_idx+1]);
		}
		
		return score;
	}
	
	function holes(playfield){
		var holes = 0;
		
		for var col_idx in 1:playfield[0].size()-1 do {
			var flag = false;
			for var row_idx in 1:playfield.size()-1 do {
				if playfield[row_idx][col_idx] != 0 then flag = true;
				if flag and playfield[row_idx][col_idx] == 0 then holes +=1;
			}
		}
		
		return holes;
	}
	
	function height(bitmap){
		var zones = [
			{"boundaries": [0,12], "value": 0},
			{"boundaries": [13,16], "value": 3},
			{"boundaries": [16, bitmap.size()], "value": 10}
		];
		
		var height = bitmap.size();
		for var row_idx in 0:bitmap.size() do {
			var empty = true;
			
			for var block in bitmap[row_idx] do {
			
				if block != 0 and block != "f" then {
	
					empty = false;
					break;
				}
			}
			
			if empty == false then {
				height = bitmap.size() - row_idx;
				break;
			}
			
		
			
			
		}
		for var zone in zones do {
				if height >= zone["boundaries"][0] and height <= zone["boundaries"][1] then
					return zone["value"];
			}
	}
	
	function keyup(e){}
	function keydown(e){}
	
	function get_incoming_lines(){
		return this.game.get_incoming_lines();	
	}
	
	function add_incoming_lines(lines){
		this.game.add_incoming_lines(lines);
	}
	
	function get_outgoing_lines(){
		return this.game.get_outgoing_lines();	
	}
	
	function set_outgoing_lines(lines){
		this.game.set_outgoing_lines(lines);
	}
}
 
# Main program
var games = [
	Game(v_canvas=[0, 0, canvas.width(), canvas.height()/2]),
	Bot(Game(v_canvas=[0, canvas.height()/2, canvas.width(), canvas.height()/2]))
];

# Timer event that contains the logic of creating a Tetromino, letting it drop over time and checking for (spawn) collisions
setEventHandler("timer", function(event) {
	for var game_idx in 0:games.size() do {
		var game = games[game_idx];
		if game.get_outgoing_lines() > 0 then {
			games[(game_idx + 1) % games.size()].add_incoming_lines(game.get_outgoing_lines());
			game.set_outgoing_lines(0);
		}
		try {
			game.step();
		}
		catch var e do {
			quitEventMode();
		}
	}
});

# Event handler that handles all keyboard inputs
setEventHandler("canvas.keydown", function(event){
	for var game in games do
		game.keydown(event.key);
});


setEventHandler("canvas.keyup", function(event){
	for var game in games do
		game.keyup(event.key);
});
enterEventMode();
